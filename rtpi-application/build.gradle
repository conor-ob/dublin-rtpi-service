import java.time.ZoneId
import java.time.ZonedDateTime
import java.time.format.DateTimeFormatter

apply plugin: 'com.github.johnrengelman.shadow'

dependencies {
    implementation project(':rtpi-api')

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.3.50"

    implementation 'io.dropwizard:dropwizard-core:1.3.14'

    implementation 'com.squareup.retrofit2:retrofit:2.6.2'
    implementation 'com.squareup.retrofit2:converter-gson:2.6.2'
    implementation("com.squareup.retrofit2:converter-simplexml:2.5.0") {
        exclude group: 'stax', module: 'stax-api'
        exclude group: 'stax', module: 'stax'
        exclude group: 'xpp3', module: 'xpp3'
    }

    implementation 'org.jsoup:jsoup:1.11.3'

    testImplementation 'io.dropwizard:dropwizard-testing:1.3.14'
    testImplementation 'org.assertj:assertj-core:3.13.2'
    testImplementation 'io.mockk:mockk:1.9.3'

    testImplementation project(path: ':rtpi-api', configuration: 'testOutput')
}

sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
    test.java.srcDirs += 'src/test/kotlin'
}

shadowJar {
    mergeServiceFiles()
    exclude 'META-INF/*.DSA', 'META-INF/*.RSA', 'META-INF/*.SF'
    manifest {
        attributes 'Implementation-Title': rootProject.name
        attributes 'Implementation-Version': rootProject.version
        attributes 'Implementation-Vendor-Id': rootProject.group
        attributes 'Build-Time': ZonedDateTime.now(ZoneId.of("UTC"))
                .format(DateTimeFormatter.ISO_ZONED_DATE_TIME)
        attributes 'Built-By': InetAddress.localHost.hostName
        attributes 'Created-By': 'Gradle ' + gradle.gradleVersion
        attributes 'Main-Class': 'ie.dublin.rtpi.RtpiServiceApplication'
    }
    archiveName 'dublin-rtpi-service.jar'
}