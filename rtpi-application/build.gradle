import java.time.ZoneId
import java.time.ZonedDateTime
import java.time.format.DateTimeFormatter

apply plugin: 'com.github.johnrengelman.shadow'

dependencies {
    implementation project(':rtpi-api')
    implementation libraries.kotlin
    implementation libraries.dropwizard_core
    implementation libraries.jsoup
    implementation libraries.retrofit
    implementation libraries.retrofit_gson
    implementation(libraries.retrofit_simplexml) {
        exclude group: 'stax', module: 'stax-api'
        exclude group: 'stax', module: 'stax'
        exclude group: 'xpp3', module: 'xpp3'
    }

    testImplementation project(path: ':rtpi-api', configuration: 'testOutput')
    testImplementation libraries.junit
    testImplementation libraries.assertj
    testImplementation libraries.mockk
    testImplementation libraries.dropwizard_testing
}

sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
    test.java.srcDirs += 'src/test/kotlin'
}

shadowJar {
    mergeServiceFiles()
    exclude 'META-INF/*.DSA', 'META-INF/*.RSA', 'META-INF/*.SF'
    manifest {
        attributes 'Implementation-Title': rootProject.name
        attributes 'Implementation-Version': rootProject.version
        attributes 'Implementation-Vendor-Id': rootProject.group
        attributes 'Build-Time': ZonedDateTime.now(ZoneId.of("UTC"))
                .format(DateTimeFormatter.ISO_ZONED_DATE_TIME)
        attributes 'Built-By': InetAddress.localHost.hostName
        attributes 'Created-By': 'Gradle ' + gradle.gradleVersion
        attributes 'Main-Class': 'ie.dublin.rtpi.RtpiServiceApplication'
    }
    archiveName 'dublin-rtpi-service.jar'
}