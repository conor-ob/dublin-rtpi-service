buildscript {
    ext.versions = [
            dropwizard: "1.3.14",
            jsoup: "1.11.3",
            kotlin: "1.3.50",
            retrofit: "2.6.2",
            rhino: '1.0',
            shadow: "5.1.0",
            threetenabp: "1.2.1",

            assertj: "3.13.2",
            junit: "4.12",
            mockk: "1.9.3"
    ]
    ext.libraries = [
            dropwizard_core: "io.dropwizard:dropwizard-core:${versions.dropwizard}",
            jsoup: "org.jsoup:jsoup:${versions.jsoup}",
            kotlin: "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${versions.kotlin}",
            retrofit: "com.squareup.retrofit2:retrofit:${versions.retrofit}",
            retrofit_gson: "com.squareup.retrofit2:converter-gson:${versions.retrofit}",
            retrofit_rx: "com.squareup.retrofit2:adapter-rxjava2:${versions.retrofit}",
            retrofit_simplexml: "com.squareup.retrofit2:converter-simplexml:${versions.retrofit}",
            rhino: "io.apisense:rhino-android:${versions.rhino}",
            threetenabp: "com.jakewharton.threetenabp:threetenabp:${versions.threetenabp}",

            assertj: "org.assertj:assertj-core:${versions.assertj}",
            dropwizard_testing: "io.dropwizard:dropwizard-testing:${versions.dropwizard}",
            junit: "junit:junit:${versions.junit}",
            mockk: "io.mockk:mockk:${versions.mockk}"
    ]
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath "com.github.jengelman.gradle.plugins:shadow:${versions.shadow}"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
    }
}

subprojects {
    apply plugin: 'kotlin'
    apply plugin: 'maven-publish'

    group = 'io.rtpi'
    version = '1-SNAPSHOT'

    repositories {
        jcenter()
        mavenCentral()
    }
    publishing {
        publications {
            maven(MavenPublication) {
                from components.java
            }
        }
    }
}

// Heroku deployment
// - git push heroku master
// or git push heroku <branch-name>:master
task stage(dependsOn: [':rtpi-application:shadowJar'])
